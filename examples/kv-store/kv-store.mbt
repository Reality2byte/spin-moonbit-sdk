// Generated by `wit-bindgen` 0.29.0.

/// This function is invoked with an incoming HTTP Request, and a resource
/// `response-outparam` which provides the capability to reply with an HTTP
/// Response. The response is sent by calling the `response-outparam.set`
/// method, which allows execution to continue after the response has been
/// sent. This enables both streaming to the response body, and performing other
/// work.
///
/// The implementor of this function must write a response to the
/// `response-outparam` before returning, or else the caller will respond
/// with an error on its behalf.
pub fn handle(
  request : @types.IncomingRequest,
  response_out : @types.ResponseOutparam
) -> Unit {
  let store = match @keyValue.Store::open("default") {
    Ok(store) => store
    _ => return @util.respond_500(response_out)
  }
  let response = match @util.split_path(request) {
      (@types.Get, ["kv-store", key]) => handle_get(store, key)
      (@types.Post, ["kv-store", key]) => handle_post(store, key, request)
      _ => return @util.respond_404(response_out)
    }
    |> Ok
  response_out.set(response)
}

fn handle_get(store : @keyValue.Store, key : String) -> @types.OutgoingResponse {
  match store.get(key) {
    Ok(Some(value)) =>
      @util.make_response(value, content_type=b"application/octet-stream")
    _ => @util.make_404_response()
  }
}

fn handle_post(
  store : @keyValue.Store,
  key : String,
  request : @types.IncomingRequest
) -> @types.OutgoingResponse {
  let value = match @util.get_body_bytes(request) {
    Ok(body) => body
    _ => return @util.make_500_response()
  }
  match store.set(key, value) {
    Ok(_) => @util.make_response(b"OK")
    _ => @util.make_404_response()
  }
}
